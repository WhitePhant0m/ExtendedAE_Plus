buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.0.91'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = project.maven_group
version = project.mod_version

base {
    archivesName = project.mod_id
}

legacyForge {
    version = project.minecraft_version + '-' + project.forge_version

    runs {
        configureEach {
            gameDirectory = project.file('run')
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
            sourceSet = sourceSets.main
        }

        server {
            server()
            sourceSet = sourceSets.main
        }

        data {
            data()
            // IDE 启动时传入参数
            programArguments.addAll(
                    '--mod', project.name, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
            )
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}
sourceSets {
    main {
        ext.refMap = "mixins.${mod_id}.refmap.json"
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "Modmaven"
        url = "https://modmaven.dev/"
    }
    maven {
        url "https://maven.shedaniel.me/"
        content {
            includeGroup "me.shedaniel"
            includeGroup "me.shedaniel.cloth"
            includeGroup "dev.architectury"
        }
    }
    maven { url "https://repo.spongepowered.org/maven" }
    maven { url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/" }
    maven { url "https://maven.bawnorton.com/releases" }
}

dependencies {
    //exendedae前置
    modImplementation "curse.maven:glodium-957920:${glodium_version}"
    //extendedAE
    modImplementation "curse.maven:ex-pattern-provider-892005:7248944"

    //ae2
    modImplementation "appeng:appliedenergistics2-forge:${ae2_version}"
    modImplementation "org.appliedenergistics:guideme:${guideme_version}"
    modImplementation "curse.maven:applied-energistics-2-wireless-terminals-459929:${wireless_terminals_version}"
    modImplementation "curse.maven:applied-flux-965012:7072853"
    modImplementation "curse.maven:mega-cells-622112:${mega_cells_version}"

    //mae2
    modRuntimeOnly "curse.maven:modern-ae2-additions-1028068:6827727"
    modCompileOnly "curse.maven:modern-ae2-additions-1028068:6827727"

    //aea
    modCompileOnly "curse.maven:advancedae-1084104:7159779"
    modRuntimeOnly "curse.maven:advancedae-1084104:7159779"

    //expae
    modCompileOnly "curse.maven:ae2addonlib-1366135:7159767"
    modCompileOnly "curse.maven:expanded-ae-1213351:7266780"
    modRuntimeOnly "curse.maven:expanded-ae-1213351:7266780"

    modCompileOnly "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"
    modRuntimeOnly "mezz.jei:jei-${minecraft_version}-forge:15.20.0.112"
    modImplementation "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"
    modImplementation "curse.maven:jade-324717:${jade_version}"

    // GregTech
    modCompileOnly "curse.maven:gregtechceu-modern-890405:${gregtech_version}"
    modCompileOnly "curse.maven:ldlib-626676:${ldlib_version}"

    //curios
    modImplementation "curse.maven:curios-309927:${curios_version}"

    // Runtime test
    modRuntimeOnly "dev.architectury:architectury-forge:9.2.14"
    modRuntimeOnly "curse.maven:cloth-config-348521:5729105"

    //jec
    modImplementation "curse.maven:just-enough-characters-250702:6680042"

    //geckolib
    modRuntimeOnly "curse.maven:geckolib-388172:6920925"
    //mclib运行时
    modRuntimeOnly "com.eliotlash.mclib:mclib:20"

    // compileOnly 供代码编译
    implementation(jarJar("curse.maven:configuration-444699:4710266"))
    modImplementation "curse.maven:configuration-444699:4710266"

    //ftbteams
    modCompileOnly "curse.maven:ftb-teams-forge-404468:6130786"
    modCompileOnly "curse.maven:ftb-library-forge-404465:6807424"
    modRuntimeOnly "curse.maven:ftb-teams-forge-404468:6130786"
    modRuntimeOnly "curse.maven:ftb-library-forge-404465:6807424"

    compileOnly "org.jetbrains:annotations:26.0.2-1"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.4.1"))
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:0.3.6-beta.1"))
    implementation(jarJar("com.github.bawnorton.mixinsquared:mixinsquared-forge:0.3.6-beta.1"))
}

compileJava {
    options.compilerArgs += ['-Xlint:-deprecation', '-Xlint:-removal', '-Xlint:-unchecked']
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             forge_version          : forge_version,
                             forge_version_range    : forge_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description,
    ]

    inputs.properties replaceProperties

    filesMatching('META-INF/mods.toml') {
        expand replaceProperties + [project: project]
    }
}

// 将 LICENSE 打包进发布的 jar
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"   : project.mod_id,
                "Specification-Vendor"  : project.mod_authors,
                "Specification-Version" : "1",
                "Implementation-Title"  : project.mod_name,
                "Implementation-Version": project.mod_version,
                "Implementation-Vendor" : project.mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }

    from('.') {
        include 'LICENSE'
    }

    finalizedBy 'reobfJar'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 17
}
tasks.withType(Jar).configureEach {
    destinationDirectory.set(file("$rootDir/jar"))
}

mixin {
    add sourceSets.main, 'mixins.extendedae_plus.refmap.json'
    config 'extendedae_plus.mixins.json'
}

jar {
    manifest {
        attributes([
                'MixinConfigs': 'extendedae_plus.mixins.json'
        ])
    }
}

// Configure Maven publishing.
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
                        distribution = 'repo'
                    }
                }
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
